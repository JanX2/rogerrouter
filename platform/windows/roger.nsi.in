; Package information (taken from configure)
!define PACKAGE_NAME "@PACKAGE_NAME@"
!define PACKAGE_VERSION "@PACKAGE_VERSION@"
!define PACKAGE_PUBLISHER "The Tabos Team"
!define PACKAGE_WEB_SITE "http://www.tabos.org"

; Include Modern UI
!include "MUI2.nsh"

; MUI Settings / Icons
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\orange-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\orange-uninstall.ico"
 
; MUI Settings / Header
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_RIGHT
!define MUI_HEADERIMAGE_BITMAP "${NSISDIR}\Contrib\Graphics\Header\orange-r.bmp"
!define MUI_HEADERIMAGE_UNBITMAP "${NSISDIR}\Contrib\Graphics\Header\orange-uninstall-r.bmp"
 
; MUI Settings / Wizard
!define MUI_WELCOMEFINISHPAGE_BITMAP "${NSISDIR}\Contrib\Graphics\Wizard\orange.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "${NSISDIR}\Contrib\Graphics\Wizard\orange-uninstall.bmp"

Name "${PACKAGE_NAME} ${PACKAGE_VERSION}"
OutFile "../../../${PACKAGE_NAME}-${PACKAGE_VERSION}.exe"

SetCompressor /SOLID lzma

; Run the installer with admin privileges
RequestExecutionLevel admin

!define MUI_ABORTWARNING

!define MUI_LANGDLL_ALLLANGUAGES

; Set default installation directory
InstallDir "$PROGRAMFILES\${PACKAGE_NAME}"

; Pages
; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_RUN	"$INSTDIR\roger.exe"
!define MUI_FINISHPAGE_RUN_NOTCHECKED
!define MUI_FINISHPAGE_LINK	"Tabos.org homepage"
!define MUI_FINISHPAGE_LINK_LOCATION	"http://www.tabos.org"
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_LANGUAGE "German" ;first language is the default language
!insertmacro MUI_LANGUAGE "English"

!insertmacro MUI_RESERVEFILE_LANGDLL

Function .onInit
	!insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

Function un.onInit
	!insertmacro MUI_UNGETLANGUAGE
FunctionEnd

Section RogerRouter
	; Install program and libraries
	SetOutPath "$INSTDIR"

	File "bin/roger.exe"
	File "bin/librm-0.dll"
	File "bin/gspawn-win32-helper-console.exe"
	File "bin/gspawn-win32-helper.exe"
	File "bin/libatk-1.0-0.dll"
	File "bin/libcairo-2.dll"
	File "bin/libcairo-gobject-2.dll"
	File "bin/libgdk-3-0.dll"
	File "bin/libepoxy-0.dll"
	File "bin/libgdk_pixbuf-2.0-0.dll"
	File "bin/libgio-2.0-0.dll"
	File "bin/libglib-2.0-0.dll"
	File "bin/libgmodule-2.0-0.dll"
	File "bin/libgobject-2.0-0.dll"
	File "bin/libgthread-2.0-0.dll"
	File "bin/libgtk-3-0.dll"
	File "bin/libiconv-2.dll"
	File "bin/libintl-8.dll"
	File "bin/libpango-1.0-0.dll"
	File "bin/libpangocairo-1.0-0.dll"
	File "bin/libpangowin32-1.0-0.dll"
	File "bin/libpixman-1-0.dll"
	File "bin/libpng16-16.dll"
	File "bin/libsndfile-1.dll"
	File "bin/libspeex-1.dll"
	File "bin/libxml2-2.dll"
	File "bin/gdk-pixbuf-query-loaders.exe"
	File "bin/zlib1.dll"
	File "bin/libjasper.dll"
	File "bin/libcapi20-3.dll"
	File "bin/libdl.dll"
	File "bin/libspandsp-2.dll"
	File "bin/libspeexdsp-1.dll"
	File "bin/libtiff-5.dll"
	File "bin/libjpeg-9.dll"
	File "bin/libffi-6.dll"
	File "bin/libpangoft2-1.0-0.dll"
	File "bin/libfontconfig-1.dll"
	File "bin/libfreetype-6.dll"
	File "bin/libexpat-1.dll"
	File "bin/libgcc_s_sjlj-1.dll"
	File "bin/libbz2.dll"
	File "bin/libharfbuzz-0.dll"
	File "bin/libsoup-2.4-1.dll"
	File "bin/libsqlite3-0.dll"
	File "bin/libcroco-0.6-3.dll"
	File "bin/librsvg-2-2.dll"
	File "bin/libwinpthread-1.dll"
	File "bin/libpcre-1.dll"
	File "bin/liblzma-5.dll"
	File "bin/libgs-9.dll"
	File "bin/libFLAC-8.dll"
	File "bin/libidn-11.dll"
	File "bin/liblcms2-2.dll"

	File "bin/libgssdp-1.0-3.dll"
	File "bin/libgupnp-1.0-4.dll"
	File "bin/libjson-glib-1.0-0.dll"

	File "bin/libgstreamer-1.0-0.dll"
	File "bin/libgstallocators-1.0-0.dll"
	File "bin/libgstaudio-1.0-0.dll"
	File "bin/libgstcontroller-1.0-0.dll"
	File "bin/libgstnet-1.0-0.dll"
	File "bin/libgstreamer-1.0-0.dll"
	File "bin/libgstrtp-1.0-0.dll"
	File "bin/libgstsdp-1.0-0.dll"
	File "bin/libgstvideo-1.0-0.dll"
	File "bin/libgstapp-1.0-0.dll"
	File "bin/libgstbase-1.0-0.dll"
	File "bin/libgstfft-1.0-0.dll"
	File "bin/libgstpbutils-1.0-0.dll"
	File "bin/libgstriff-1.0-0.dll"
	File "bin/libgstrtsp-1.0-0.dll"
	File "bin/libgsttag-1.0-0.dll"
	File "bin/libogg-0.dll"
	;File "bin/liborc-0.4-0.dll"
	File "bin/libvorbis-0.dll"
	File "bin/libvorbisenc-2.dll"
	File "bin/libwavpack-1.dll"

	# SSL
	File "bin/libgnutls-30.dll"
	File "bin/libgmp-10.dll"
	File "bin/libhogweed-4.dll"
	File "bin/libidn2-0.dll"
	File "bin/libnettle-6.dll"
	File "bin/libunistring-2.dll"

	SetOutPath "$INSTDIR\lib\gstreamer-1.0"
	;File /r "/usr/i686-w64-mingw32/sys-root/mingw/lib/gstreamer-1.0/"
	File "bin/gstreamer-1.0/libgstapp.dll"
	File "bin/gstreamer-1.0/libgstaudioconvert.dll"
	File "bin/gstreamer-1.0/libgstaudioresample.dll"
	File "bin/gstreamer-1.0/libgstautodetect.dll"
	File "bin/gstreamer-1.0/libgstcoreelements.dll"
	File "bin/gstreamer-1.0/libgstdirectsoundsink.dll"
	File "bin/gstreamer-1.0/libgstdirectsoundsrc.dll"
	File "bin/gstreamer-1.0/libgstplayback.dll"

	SetOutPath "$INSTDIR\lib\gio"
	File /r "lib/gio/"

	; Install data files
	SetOutPath "$INSTDIR\etc"
	File /r "etc/fonts"
	File /r "etc/gtk-3.0"
	CreateDirectory "$INSTDIR\etc\pango"

	SetOutPath "$INSTDIR\etc\gtk-3.0"
	File "settings.ini"

	SetOutPath "$INSTDIR\lib"
	File /r "lib/gdk-pixbuf-2.0"

	SetOutPath "$INSTDIR\lib\capi"
	File /r "lib/capi/*fritz*.dll"

	SetOutPath "$INSTDIR\lib\roger"
	File /r "lib/roger/*.dll"
	File /r "lib/roger/*.plugin"

	SetOutPath "$INSTDIR\lib\rm"
	File /r "lib/rm/"

	SetOutPath "$INSTDIR\share\"
	File /r "share/glib-2.0"

	; Themes / Icons
	SetOutPath "$INSTDIR\share\icons\Adwaita\scalable"
	File /r "share/icons/Adwaita/scalable/*"

	SetOutPath "$INSTDIR\share\icons\Adwaita"
	File "share/icons/Adwaita/index.theme"

	SetOutPath "$INSTDIR\share\icons\hicolor"
	File /r "share/icons/hicolor/*"

	; Translations
	SetOutPath "$INSTDIR\share\locale\de\LC_MESSAGES\"
	File "share/locale/de/LC_MESSAGES/rm.mo"
	File "share/locale/de/LC_MESSAGES/roger.mo"
	File "share/locale/de/LC_MESSAGES/atk10.mo"
	File "share/locale/de/LC_MESSAGES/gtk30.mo"

	ReadEnvStr $0 COMSPEC

	SetOutPath "$INSTDIR"
	DetailPrint "Creating loaders.cache"
	nsExec::ExecToLog '"$0" /C .\gdk-pixbuf-query-loaders.exe > lib\gdk-pixbuf-2.0\2.10.0\loaders.cache'

	WriteUninstaller "$INSTDIR\uninstall.exe"

	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\RogerRouter" "DisplayName" "Roger Router"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\RogerRouter" "DisplayVersion" "${PACKAGE_VERSION}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\RogerRouter" "Publisher" "Tabos.org"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\RogerRouter" "HelpLink" "http://www.tabos.org"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\RogerRouter" "URLInfoAbout" "http://www.tabos.org"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\RogerRouter" "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
SectionEnd

Section "Start Menu Shortcuts"
	CreateShortCut "$SMPROGRAMS\Roger Router.lnk" "$INSTDIR\roger.exe"
SectionEnd

Section "Desktop Icons"
	CreateShortCut "$DESKTOP\Roger Router.lnk" "$INSTDIR\roger.exe"
SectionEnd

Section "Uninstall"
	DeleteRegKey HKCU "Software\GSettings\org\tabos"
	Delete /rebootok "$DESKTOP\Roger Router.lnk"
	Delete /rebootok "$SMPROGRAMS\RogerRouter\Roger Router.lnk"
	Delete /rebootok "$SMPROGRAMS\RogerRouter\Uninstall.lnk"
	RMDir "$SMPROGRAMS\RogerRouter"
	RMDir /r "$INSTDIR"

	; Removing printers
	SetDetailsPrint textonly
	DetailPrint "Removing Roger Router Fax printer"
	SetDetailsPrint none
	ExecWait 'rundll32 printui.dll,PrintUIEntry /dl /n "Roger Router (Fax)" /q'

	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\RogerRouter"
SectionEnd

;Add printer ports to registry
Section -RegistryFiles SEC05
	SetDetailsPrint textonly
	DetailPrint "Adding printer port to registry"
	SetDetailsPrint none
 
	;Include the results from Reg2NSIS utility
	!include printer.nsh
	sleep 1
SectionEnd

;Restart Print Spooler service (needed to load new ports)
Section -RestartSpooler SEC10
	;Display text above progress bar
	SetDetailsPrint textonly
	DetailPrint "Restarting Print Spooler service"
	SetDetailsPrint none
 
	;Run command to restart services
	nsExec::Exec "net stop Spooler"
	sleep 2
	nsExec::Exec "net start Spooler"
	sleep 2
SectionEnd

;Execute rundll to install printers
; NOTE: ExecWait requires quotes in single-double-double-single format (' " " ') for this to work
Section -ExecBatchFiles SEC15
	SetOutPath $INSTDIR
 
	;Display text above progress bar
	SetDetailsPrint textonly
	DetailPrint "Removing existing Roger Router Fax printer"
	SetDetailsPrint none

	; Removing printers
	ExecWait 'rundll32 printui.dll,PrintUIEntry /q /dl /n "Roger Router (Fax)"'
	sleep 2

	;Display text above progress bar
	SetDetailsPrint textonly
	DetailPrint "Installing new Roger Router Fax printer"
	SetDetailsPrint none

	; Installing printers
	ExecWait 'rundll32 printui.dll,PrintUIEntry /if /b "Roger Router (Fax)" /f "C:\Windows\INF\prnms005.inf" /r "RogerRouterFax" /m "Microsoft PS Class Driver" /z'
	sleep 1

	;Display text above progress bar
	SetDetailsPrint textonly
	DetailPrint "Applying details to new Roger Router Fax printer"
	SetDetailsPrint none

	; Adding Printer comments
	ExecWait 'rundll32 printui.dll,PrintUIEntry /q /Xs /n "Roger Router (Fax)" comment "Fax-Printer of Roger Router"  location "Router"'

	SetDetailsPrint textonly
	DetailPrint "Installation completed"
	SetDetailsPrint none
SectionEnd
